#include <SPI.h>
#include <MFRC522.h>
#include <WiFi.h>
#include <ESP32Servo.h>
#include <Arduino.h>
#include <ArduinoJson.h>
#include <time.h>

/*--------------------------------WiFi ì„¤ì •--------------------------------*/

const char* ssid = "addinedu_class_2 (2.4G)";
const char* password = "addinedu1";

/*--------------------------------PC ì„œë²„ ì£¼ì†Œ ë° í¬íŠ¸--------------------------------*/

IPAddress serverIP(192, 168, 0, 166);  // â† PC IPë¡œ ë°”ê¾¸ì„¸ìš”
const int serverPort = 8001;  
WiFiClient client;
String incoming_msg = "";

/*--------------------------------ë“±ë¡ëœ UID ëª©ë¡--------------------------------*/

struct UIDEntry 
{
  byte uid[4];
  const char* description;
};

UIDEntry registeredCards[] = {
  // { {0x8B, 0xEE, 0xC9, 0x01}, "CHARGE_LOCATION" },
  { {0x86, 0x51, 0x0A, 0x05}, "CHECKPOINT_A" },
  { {0x12, 0x6D, 0x07, 0x05}, "CHECKPOINT_B" },
  { {0xD9, 0x3F, 0x09, 0x05}, "load_A" },
  { {0xA3, 0x8F, 0x09, 0x05}, "load_B" },
  { {0x9C, 0x84, 0x0B, 0x05}, "CHECKPOINT_C" },
  { {0x83, 0x58, 0xAE, 0x1A}, "BELT" },
  { {0x63, 0x9D, 0x9F, 0x35}, "CHECKPOINT_D" },
  { {0xF3, 0x16, 0x63, 0x1B}, "STANDBY" },
  
};
const int numRegistered = sizeof(registeredCards) / sizeof(registeredCards[0]);

unsigned long last_rfid_check = 0;
const unsigned long RFID_CHECK_INTERVAL = 300;  // 0.3ì´ˆë§ˆë‹¤ RFID ì²´í¬

/*--------------------------------íŠ¸ëŸ­ ID ì„¤ì •--------------------------------*/

char* truck_id = "TRUCK_01";

/*-------------------------ìƒíƒœ ë¡œì§ ë³€í™˜ ë° ê¸°íƒ€ ë³€ìˆ˜ë“¤--------------------------------*/

bool run_command = false;
bool obstacle_block = false;   //ì§€ê¸ˆ ë©ˆì¶°ì•¼ í•˜ë‚˜?(ì‹¤ì‹œê°„ ê²°ì •ìš©)
bool prev_obstacle_state = false;
float last_distance_cm = 0;

String current_position = "UNKNOWN";
String last_cmd = "";
String mission_target = "";
unsigned long last_mission_check = 0;    // ë§ˆì§€ë§‰ ë¯¸ì…˜ ì²´í¬ ì‹œê°„
const unsigned long MISSION_CHECK_INTERVAL = 5000;  // 5ì´ˆë§ˆë‹¤ ì²´í¬

/*-------------------------loading ë³€ìˆ˜ë“¤--------------------------------*/

bool wait_start_loading = false;
unsigned long wait_start_loading_time = 0;

bool loading_in_progress = false;
unsigned long loading_start_time = 0;

/*-------------------------unloading ë³€ìˆ˜ë“¤--------------------------------*/
bool wait_start_unloading = false;
unsigned long wait_start_unloading_time = 0;

bool unloading_in_progress = false;
unsigned long unloading_start_time = 0;

unsigned long unloading_stage_time = 0;
int unloading_stage = 0;

// ì„œë³´ëª¨í„° ì œì–´ ê´€ë ¨ ì „ì—­ ë³€ìˆ˜
Servo unloading_servo;
const int SERVO_PIN = 17;
const int SERVO_INIT_ANGLE = 170;
const int SERVO_DROP_ANGLE = 90;

/*--------------------------------ê°€ìƒ ë°°í„°ë¦¬ ì”ëŸ‰ ì²´í¬--------------------------------*/

int battery_level = 100;

unsigned long last_battery_drop = 0;
const unsigned long BATTERY_DROP_INTERVAL = 5000; //5ì´ˆ ë§ˆë‹¤ ë°°í„°ë¦¬ ê°ì†Œ 

unsigned long last_battery_report = 0;
const unsigned long BATTERY_REPORT_INTERVAL = 5000; // 5ì´ˆë§ˆë‹¤ ë°°í„°ë¦¬ ìƒíƒœ ì„œë²„ì— ì „ì†¡
bool battery_empty = false;  // ë°°í„°ë¦¬ 0% ìƒíƒœ í”Œë˜ê·¸


/*--------------------------------ëª¨í„° ì œì–´ í•€ ë° PWM--------------------------------*/

#define MOTOR12_EN 27    // PWM ì±„ë„ 0
#define MOTOR34_EN 13    // PWM ì±„ë„ 1
#define MOTOR1_IN1 26
#define MOTOR1_IN2 25
#define MOTOR2_IN3 12
#define MOTOR2_IN4 14

#define PWM_FREQ 1000
#define PWM_RESOLUTION 8
#define PWM_CHANNEL_LEFT 0
#define PWM_CHANNEL_RIGHT 1

/*--------------------------------ì´ˆìŒíŒŒ ì„¼ì„œ í•€--------------------------------*/

#define TRIG_PIN 33
#define ECHO_PIN 32

/*--------------------------------rfid ì„¼ì„œ í•€--------------------------------*/

#define SS_PIN 21    // SDA
#define RST_PIN 22   // RST

/*--------------------------------ì ì™¸ì„  ì„¼ì„œ í•€--------------------------------*/
#define LEFT_SENSOR 34
#define RIGHT_SENSOR 35

/*--------------------------------PID ì œì–´ ë³€ìˆ˜--------------------------------*/

double Kp = 0.1020;
double Kd = 0.2;
double Ki = 0.0001;       
double integral = 0.0;  // ëˆ„ì  ì ë¶„ê°’
double PID_control;
int last_error = 0;
int derivative;
int L_PWM, R_PWM;
int error;
int l_sensor_val;
int r_sensor_val;
int avg_PWM = 150;
int max_pwm = 75;

/*--------------------------------rfid ê°ì²´ ìƒì„±--------------------------------*/

MFRC522 rfid(SS_PIN, RST_PIN);

/*--------------------------------í•¨ìˆ˜ ì„ ì–¸--------------------------------*/

void receive_json(const String& msg);
void send_obstacle(float distance_cm, bool detected, const char* position);
void send_arrived(const char* position, const char* gate_id);
bool isSameUID(byte* uid1, byte* uid2);
bool checkAndPrintUID(byte* uid);

/*--------------------------------------------------------------------------------*/

void setup() 
{
  Serial.begin(115200);

  // ëª¨í„° í•€ ì„¤ì •
  pinMode(MOTOR1_IN1, OUTPUT);
  pinMode(MOTOR1_IN2, OUTPUT);
  pinMode(MOTOR2_IN3, OUTPUT);
  pinMode(MOTOR2_IN4, OUTPUT);

  ledcSetup(PWM_CHANNEL_LEFT, PWM_FREQ, PWM_RESOLUTION);
  ledcAttachPin(MOTOR12_EN, PWM_CHANNEL_LEFT);
  ledcSetup(PWM_CHANNEL_RIGHT, PWM_FREQ, PWM_RESOLUTION);
  ledcAttachPin(MOTOR34_EN, PWM_CHANNEL_RIGHT);
  
  // ì´ˆìŒíŒŒì„¼ì„œ í•€ ì„¤ì •
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);

  //ì„œë³´ëª¨í„° ì´ˆê¸° ì„¤ì •
  unloading_servo.attach(SERVO_PIN);
  unloading_servo.write(SERVO_INIT_ANGLE);  // ì´ˆê¸° ìœ„ì¹˜

  // WiFi ì—°ê²°
  WiFi.begin(ssid, password);
  Serial.println("WiFi ì—°ê²° ì¤‘...");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nâœ…Wi-Fi ì—°ê²° ì™„ë£Œ!");

  // ì„œë²„ ì ‘ì† ì‹œë„
  reconnectToServer();

  // RFID ì´ˆê¸°í™”
  SPI.begin(18, 19, 23, 21);  // SCK, MISO, MOSI, SS
  rfid.PCD_Init();
  Serial.println("âœ…RC522 RFID ë¦¬ë”ê¸° ì‹œì‘ë¨!");

  // ì‹œê°„ ë™ê¸°í™”
  configTime(9 * 3600, 0, "pool.ntp.org", "time.nist.gov");
  Serial.println("â³ ì‹œê°„ ë™ê¸°í™” ëŒ€ê¸° ì¤‘...");
  while (time(nullptr) < 100000) 
  {
    delay(100);
    Serial.println("...");
  }
  Serial.println("âœ…ì‹œê°„ ë™ê¸°í™” ì™„ë£Œ!");

  // ë¯¸ì…˜ ìš”ì²­ ìë™ ì „ì†¡
  delay(2000);  // ì•ˆì •í™” ëŒ€ê¸°
  send_assign_mission();

}

void loop() 
{
  reconnectToServer();

  // âœ… ìˆ˜ì‹  ë©”ì‹œì§€ ì²˜ë¦¬
  if (client && client.available()) {
    incoming_msg = client.readStringUntil('\n');
    incoming_msg.trim();

    Serial.println("========== ğŸ“© [ì„œë²„ ë©”ì‹œì§€ ìˆ˜ì‹ ] ==========");
    Serial.println(incoming_msg);
    Serial.println("===========================================");

    receive_json(incoming_msg);
  }

  // âœ… í˜„ì¬ ì‹œê°„ ê°±ì‹ 
  unsigned long current_time = millis();

  // âœ… ì£¼ê¸°ì ì¸ ë¯¸ì…˜ ì²´í¬
  if (current_time - last_mission_check >= MISSION_CHECK_INTERVAL) 
  {
    last_mission_check = current_time;
    if (current_position == "UNKNOWN" || current_position == "STANDBY") 
    {
      Serial.println("[ğŸ”„ ë¯¸ì…˜ ì²´í¬] ìƒˆë¡œìš´ ë¯¸ì…˜ í™•ì¸ ì¤‘...");
      send_assign_mission();
    }
  }

    // âœ… ì£¼í–‰ ì œì–´
  obstacle_block = obstacle_detected();
  if (run_command && !obstacle_block && !battery_empty)
  {
    //Serial.println("run");
    line_trace();
    send_obstacle(last_distance_cm, false, current_position.c_str());
  }
  else if (obstacle_block) 
  {
    Serial.println("stop");
    //Serial.print("Distance: ");
    //Serial.print(distance_cm);
    //Serial.println(" cm");
    stop_motors();
    send_obstacle(last_distance_cm, true, current_position.c_str());
  }
  //ì ì¬ ì‹œì‘ ì§€ì—° ì²˜ë¦¬
  if (wait_start_loading && (current_time - wait_start_loading_time >= 2000)) 
  {
    Serial.println("ğŸ•’ ì ì¬ ì‹œì‘ ë©”ì‹œì§€ ì „ì†¡ (2ì´ˆ ì§€ì—° í›„)");
    send_start_loading();
    loading_in_progress = true;
    loading_start_time = current_time;
    wait_start_loading = false;
  }
  // ì ì¬ ì™„ë£Œ ë¡œì§ ì¶”ê°€ (5ì´ˆ ë’¤ ìë™ ì „ì†¡)
  if (loading_in_progress && (current_time - loading_start_time >= 5000)) 
  {
    Serial.println("âœ… ì ì¬ ì™„ë£Œ ë©”ì‹œì§€ ì „ì†¡ (5ì´ˆ ê²½ê³¼)");
    send_finish_loading();
    loading_in_progress = false;
  }

  // ì–¸ë¡œë”© ì‹œì‘ ì§€ì—° ì²˜ë¦¬
  if (wait_start_unloading && (current_time - wait_start_unloading_time >= 2000)) 
  {
    start_unloading();
    wait_start_unloading = false;
  }

  // ì–¸ë¡œë”© FSM ì²˜ë¦¬
  handle_unloading(current_time);
    
  // RFID ì²´í¬
  if (!rfid.PICC_IsNewCardPresent() || !rfid.PICC_ReadCardSerial()) 
  {
    return;
  }

  // Serial.print("UID: ");
  // for (byte i = 0; i < rfid.uid.size; i++) {
  //   if (rfid.uid.uidByte[i] < 0x10) Serial.print("0");
  //   Serial.print(rfid.uid.uidByte[i], HEX);
  //   if (i < rfid.uid.size - 1) Serial.print("-");
  // }
  // Serial.println();

  // UID í™•ì¸ ë° ì„œë²„ ì „ì†¡
  checkAndPrintUID(rfid.uid.uidByte);

  // ğŸª« 10ì´ˆë§ˆë‹¤ ë°°í„°ë¦¬ ê°ì†Œ
  if (current_time - last_battery_drop >= BATTERY_DROP_INTERVAL) {
    last_battery_drop = current_time;

    if (battery_level > 0) {
      battery_level -= 5;
      if (battery_level <= 0) {
        battery_level = 0;
        battery_empty = true;
        run_command = false;
        stop_motors();
        Serial.println("âŒ ë°°í„°ë¦¬ ì†Œì§„ â†’ íŠ¸ëŸ­ ì •ì§€");
      }

      Serial.print("ğŸª« ë°°í„°ë¦¬ ê°ì†Œë¨: ");
      Serial.print(battery_level);
      Serial.println("%");
    }
  }

  // ğŸ“¤ 5ì´ˆë§ˆë‹¤ ì„œë²„ì— ë°°í„°ë¦¬ ìƒíƒœ ì „ì†¡
  if (current_time - last_battery_report >= BATTERY_REPORT_INTERVAL) {
    last_battery_report = current_time;
    send_battery_status();
  }

  rfid.PICC_HaltA();
  rfid.PCD_StopCrypto1();

}

/*------------------------------- ìˆ˜ì‹  ì²˜ë¦¬--------------------------------*/

void receive_json(const String& msg)
{
  Serial.println("ğŸ“© [ë””ë²„ê¹…] receive_json() í˜¸ì¶œë¨");  // âœ”ï¸

  Serial.print("ğŸ“© [ë””ë²„ê¹…] ì›ë³¸ ë©”ì‹œì§€: ");
  Serial.println(msg);  // âœ”ï¸

  StaticJsonDocument<256> doc;
  DeserializationError err = deserializeJson(doc, msg);

  if (err)
  {
    Serial.println("âš ï¸ [JSON íŒŒì‹± ì‹¤íŒ¨]");
    Serial.println(err.c_str());  // âœ”ï¸ êµ¬ì²´ì ì¸ íŒŒì‹± ì—ëŸ¬ ì¶œë ¥
    return;
  }

  Serial.println("âœ… [JSON íŒŒì‹± ì„±ê³µ]");  // âœ”ï¸

  const char* cmd = doc["cmd"];
  Serial.print("ğŸ“© [ë””ë²„ê¹…] íŒŒì‹±ëœ ëª…ë ¹ì–´: ");
  Serial.println(cmd);

  if (last_cmd == String(cmd))
  {
    Serial.print("[â­ï¸ ì¤‘ë³µ ëª…ë ¹ ë¬´ì‹œ] ì´ë¯¸ ì²˜ë¦¬í•œ ëª…ë ¹: ");  
    Serial.println(cmd);
    return;
  }

  last_cmd = String(cmd);
  
  // ëª…ë ¹ ì²˜ë¦¬
  if (strcmp(cmd, "SET_SPEED") == 0) 
  {
    Serial.println("[ë””ë²„ê¹…] SET_SPEED ëª…ë ¹ ì²˜ë¦¬ ì‹œì‘");
  } 
  else if (strcmp(cmd, "RUN") == 0) 
  {
    Serial.println("[âœ… ë””ë²„ê¹…] RUN ëª…ë ¹ ìˆ˜ì‹ ë¨!");
    run_command = true;
  } 
  else if (strcmp(cmd, "STOP") == 0) 
  {
    Serial.println("[â›” ë””ë²„ê¹…] STOP ëª…ë ¹ ìˆ˜ì‹ ë¨!");
    run_command = false;
    stop_motors();
  } 
  else if (strcmp(cmd, "GATE_OPENED") == 0) 
  {
  const char* gate_id = doc["payload"]["gate_id"];
  Serial.print("[ğŸšª ê²Œì´íŠ¸ ì—´ë¦¼ ê°ì§€] : ");
  Serial.println(gate_id);

  send_gateopen_message(gate_id);  // ì‘ë‹µ ì „ì†¡
  run_command = true;
  }
  else if (strcmp(cmd, "MISSION_ASSIGNED") == 0) 
  {
    const char* target = doc["payload"]["source"];
    mission_target = String(target);
    Serial.print("[ğŸ“¦ ë¯¸ì…˜ í• ë‹¹ë¨] ëª©í‘œ ìœ„ì¹˜: ");
    Serial.println(mission_target);
    run_command = true;
  }
  else if (strcmp(cmd, "NO_MISSION") == 0) 
  {
    Serial.println("ğŸ“­ [ì„œë²„ ì‘ë‹µ] ë¯¸ì…˜ ì—†ìŒ â†’ ëŒ€ê¸° ìƒíƒœ ìœ ì§€");
    run_command = false;
    mission_target = "";
  }
  else 
  {
    Serial.print("[â„¹ï¸ ë””ë²„ê¹…] ì•Œ ìˆ˜ ì—†ëŠ” ëª…ë ¹: ");
    Serial.println(cmd);
  }
}


/*-------------------------------- ì†¡ì‹  ì²˜ë¦¬ --------------------------------*/

// JSON ì†¡ì‹  í•¨ìˆ˜
void send_json(const char* cmd, JsonObject payload)
{
  StaticJsonDocument<256> doc;

  // ê³µí†µ ë©”ì‹œì§€ êµ¬ì¡°
  doc["sender"] = truck_id;
  doc["receiver"] = "SERVER";
  doc["cmd"] = cmd;
  doc["payload"] = payload;
  
  // ì„œë²„ ì—°ê²° í™•ì¸ í›„ ë©”ì‹œì§€ ì „ì†¡
  if (client && client.connected())
  {
    serializeJson(doc, client);
    client.print("\n");
    Serial.println("[ğŸ“¤ ì†¡ì‹ ] ë©”ì‹œì§€ ì „ì†¡:");
    serializeJsonPretty(doc, Serial);
    Serial.println();
  }
  else
  {
    Serial.println("[âŒ ì˜¤ë¥˜] ì„œë²„ì™€ ì—°ê²°ë˜ì§€ ì•ŠìŒ (ë©”ì‹œì§€ ì „ì†¡ ì‹¤íŒ¨)");
  }
}

// ë¯¸ì…˜ ìš”ì²­ ë©”ì‹œì§€ (ASSIGN_MISSION)
void send_assign_mission() 
{
  StaticJsonDocument<256> doc;
  JsonObject payload = doc.createNestedObject("payload");
  send_json("ASSIGN_MISSION", payload);
}

void send_gateopen_message(const char* gate_id)
{
  StaticJsonDocument<256> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["gate_id"] = gate_id;
  payload["position"] = current_position;
  payload["timestamp"] = getISOTime();

  send_json("ACK_GATE_OPENED", payload);
}


// ë„ì°© ë©”ì‹œì§€ (ARRIVED)
void send_arrived(const char* position, const char* gate_id) 
{
  StaticJsonDocument<256> doc;
  JsonObject payload = doc.createNestedObject("payload");
  payload["position"] = position;
  payload["gate_id"] = gate_id;
  payload["timestamp"] = getISOTime();
  send_json("ARRIVED", payload);
}

// ì¥ì• ë¬¼ ê°ì§€ ë©”ì‹œì§€ (OBSTACLE)
void send_obstacle(float distance_cm, bool detected, const char* position) 
{

  if (detected == prev_obstacle_state) return;

  prev_obstacle_state = detected;

  StaticJsonDocument<256> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["position"] = position;
  payload["distance_cm"] = distance_cm;
  payload["timestamp"] = getISOTime();
  payload["detected"] = detected ? "DETECTED" : "CLEARED";
  
  send_json("OBSTACLE", payload);
}

//ë¡œë”© ì‹œì‘ ë©”ì„¸ì§€
void send_start_loading() 
{
  StaticJsonDocument<128> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["position"] = current_position;
  payload["timestamp"] = getISOTime();

  send_json("START_LOADING", payload);
}

void send_finish_loading() 
{
  StaticJsonDocument<128> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["position"] = current_position;
  payload["timestamp"] = getISOTime();

  send_json("FINISH_LOADING", payload);
}
//ì–¸ë¡œë”© ì‹œì‘ ë©”ì„¸ì§€
void send_start_unloading() {
  StaticJsonDocument<128> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["position"] = current_position;
  payload["timestamp"] = getISOTime();

  send_json("START_UNLOADING", payload);
}

void send_finish_unloading() {
  StaticJsonDocument<128> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["position"] = current_position;
  payload["timestamp"] = getISOTime();

  send_json("FINISH_UNLOADING", payload);
}


void send_battery_status() {
  StaticJsonDocument<128> doc;
  JsonObject payload = doc.createNestedObject("payload");

  payload["battery_level"] = battery_level;
  payload["timestamp"] = getISOTime();

  send_json("BATTERY", payload);
}


/*--------------------------------ë¼ì¸íŠ¸ë ˆì´ì„œ ì œì–´--------------------------------*/

void line_trace() {
  l_sensor_val = analogRead(LEFT_SENSOR);
  r_sensor_val = analogRead(RIGHT_SENSOR);

  Serial.print("L: "); Serial.print(l_sensor_val);
  Serial.print(" R: "); Serial.println(r_sensor_val);

  error = l_sensor_val - r_sensor_val;


  // â¬‡ PID ì œì–´ ê³„ì‚°
  integral += error;
  derivative = error - last_error;
  PID_control = Kp * error + Ki * integral + Kd * derivative;


  last_error = error;

  R_PWM = speed_limit(avg_PWM - PID_control, 0, max_pwm);
  L_PWM = speed_limit(avg_PWM + PID_control, 0, max_pwm);

  left_motor_f(L_PWM);
  right_motor_f(R_PWM);
}

void stop_motors() {
  ledcWrite(PWM_CHANNEL_LEFT, 0);
  ledcWrite(PWM_CHANNEL_RIGHT, 0);
}

void left_motor_f(int pwm_val) {
  digitalWrite(MOTOR1_IN1, LOW);
  digitalWrite(MOTOR1_IN2, HIGH);
  ledcWrite(PWM_CHANNEL_LEFT, pwm_val);
}

void right_motor_f(int pwm_val) {
  digitalWrite(MOTOR2_IN3, LOW);
  digitalWrite(MOTOR2_IN4, HIGH);
  ledcWrite(PWM_CHANNEL_RIGHT, pwm_val);
}

int speed_limit(int val, int minVal, int maxVal) {
  if (val < minVal) return minVal;
  if (val > maxVal) return maxVal;
  return val;
}

/*--------------------------------ì´ˆìŒíŒŒ ê¸°ë°˜ ì¥ì• ë¬¼ ê°ì§€--------------------------------*/

// ì¥ì• ë¬¼ ê°ì§€ ì—¬ë¶€
bool obstacle_detected() {
  long duration;
  float distance_cm;

  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);

  duration = pulseIn(ECHO_PIN, HIGH); 
  if (duration == 0)
  {
    //Serial.println("Hello");
    return false;  // ì‹¤íŒ¨í–ˆìœ¼ë©´ ì¥ì• ë¬¼ ì—†ìŒ
  }
  
  distance_cm = duration * 0.034 / 2.0;  // ê±°ë¦¬ ê³„ì‚°
  last_distance_cm = distance_cm;  // ì „ì—­ ë³€ìˆ˜ ì—…ë°ì´íŠ¸

  return distance_cm < 12.0;  // 10cm ì´ë‚´ë©´ true
}
/*--------------------------------ì–¸ë¡œë”© ì²˜ë¦¬ í•¨ìˆ˜--------------------------------*/

void start_unloading() {
  Serial.println("ğŸ•’ ì–¸ë¡œë”© ì‹œì‘ ë©”ì‹œì§€ ì „ì†¡ (2ì´ˆ ì§€ì—° í›„)");
  send_start_unloading();
  unloading_in_progress = true;
  unloading_stage = 1;
  unloading_stage_time = millis();
}


void handle_unloading(unsigned long current_time) {
  if (!unloading_in_progress) return;

  if (unloading_stage == 1 && current_time - unloading_stage_time >= 0) {
    Serial.println("âœ… ì–¸ë¡œë”© ì„œë³´ëª¨í„° â†’ 90ë„ (ë‚´ë¦¬ê¸°)");
    unloading_servo.write(SERVO_DROP_ANGLE);
    unloading_stage_time = current_time;
    unloading_stage = 2;
  }
  else if (unloading_stage == 2 && current_time - unloading_stage_time >= 2000) {
    Serial.println("âœ… ì–¸ë¡œë”© ì„œë³´ëª¨í„° â†’ 170ë„ (ì˜¬ë¦¬ê¸°)");
    unloading_servo.write(SERVO_INIT_ANGLE);
    unloading_stage_time = current_time;
    unloading_stage = 3;
  }
  else if (unloading_stage == 3 && current_time - unloading_stage_time >= 1000) {
    Serial.println("âœ… ì–¸ë¡œë”© ì™„ë£Œ ë©”ì‹œì§€ ì „ì†¡");
    send_finish_unloading();
    unloading_in_progress = false;
    unloading_stage = 0;
  }
}


/*--------------------------------UID ê´€ë ¨ í•¨ìˆ˜--------------------------------*/

bool isSameUID(byte *uid1, byte *uid2) 
{
  for (byte i = 0; i < 4; i++) 
  {
    if (uid1[i] != uid2[i]) return false;
  }
  return true;
}

bool checkAndPrintUID(byte* uid) 
{
  for (int i = 0; i < numRegistered; i++) {
    if (isSameUID(uid, registeredCards[i].uid)) {
      const char* desc = registeredCards[i].description;

      Serial.println("âœ… ë“±ë¡ëœ ì¹´ë“œì…ë‹ˆë‹¤!");
      Serial.print("ğŸ“Œ ");
      Serial.println(desc);

      // ìœ„ì¹˜ ê°±ì‹  ë° ë„ì°© ë©”ì‹œì§€ ì „ì†¡
      current_position = String(desc);

      if (strcmp(desc, "CHECKPOINT_A") == 0) 
      {
        send_arrived("CHECKPOINT_A", "GATE_A");
        run_command = false;
      } 
      else if (strcmp(desc, "CHECKPOINT_B") == 0) 
      {
        send_arrived("CHECKPOINT_B", "GATE_A");
      } 
      else if (strcmp(desc, "CHECKPOINT_C") == 0) 
      {
        send_arrived("CHECKPOINT_C", "GATE_B");
        run_command = false;
      } 
      else if (strcmp(desc, "CHECKPOINT_D") == 0) 
      {
        send_arrived("CHECKPOINT_D", "GATE_B");
      }
      else if (strcmp(desc, "load_A") == 0)                        //load_A
      {
        send_arrived("load_A", "LOAD_A");
        // í˜„ì¬ ëª©ì ì§€ê°€ load_Aì¸ ê²½ìš°ì—ë§Œ ì ì¬ ì‹œì‘ ëŒ€ê¸°
        if ((mission_target == "LOAD_A")or(mission_target == "load_A")) 
        {
          Serial.println(mission_target);
          Serial.println("Debug1");
          wait_start_loading = true;
          wait_start_loading_time = millis();
        }
      }
      else if (strcmp(desc, "load_B") == 0)                       //load_B
      {
        send_arrived("load_B", "LOAD_B");

        if ((mission_target == "load_B") or (mission_target == "LOAD_B")) 
        {
          wait_start_loading = true;
          wait_start_loading_time = millis();
        }
      }
      else if (strcmp(desc, "BELT") == 0) 
      {
        send_arrived("BELT", "BELT");
        wait_start_unloading = true;
        wait_start_unloading_time = millis();
        
      }
      else if (strcmp(desc, "STANDBY") == 0) 
      {
        send_arrived("STANDBY", "STANDBY");
        run_command = false;
        stop_motors();
        send_assign_mission(); 
      } 


      // ğŸ¯ ëª©ì ì§€ì— ë„ë‹¬í•œ ê²½ìš° ë©ˆì¶¤
      if (mission_target != "" && mission_target == String(desc)) {
        Serial.println("ğŸ¯ [ë„ì°© í™•ì¸] ëª©ì ì§€ ë„ë‹¬ â†’ ì£¼í–‰ ì¤‘ì§€");
        run_command = false;
        stop_motors();
      }

      return true;
    }
  }

  Serial.println("âŒ ë“±ë¡ë˜ì§€ ì•Šì€ ì¹´ë“œì…ë‹ˆë‹¤!");
  return false;  // ë“±ë¡ë˜ì§€ ì•ŠìŒ
}

/*-------------------------------ìœ í‹¸ í•¨ìˆ˜--------------------------------*/

String getISOTime() 
{
  time_t now = time(nullptr);
  struct tm* t = localtime(&now);
  char buffer[25];
  strftime(buffer, sizeof(buffer), "%Y-%m-%dT%H:%M:%S", t);
  return String(buffer);
}

void reconnectToServer() 
{
  if (!client.connected()) 
  {
    Serial.print("ğŸŒ ì„œë²„ ì ‘ì† ì‹œë„ ì¤‘...");
    if (client.connect(serverIP, serverPort)) 
    {
      Serial.println("âœ… ì ‘ì† ì„±ê³µ!");
    } 
    else 
    {
      Serial.println("âŒ ì ‘ì† ì‹¤íŒ¨");
    }
  }
}
